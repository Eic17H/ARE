.constant
	OBJREF 0x40
	endline 0x3b
.end-constant

.main
    .var
        x
        y
        z
        x_piu_y
        fun2_y
        fun1_xpiuy_fun2y
        fun1_piu_fun2
    .end-var

        LDC_W OBJREF
        INVOKEVIRTUAL input
    ISTORE x
        LDC_W OBJREF
        INVOKEVIRTUAL input
    ISTORE y
        LDC_W OBJREF
        INVOKEVIRTUAL input
    ISTORE z

            ILOAD x
            ILOAD y
        IADD
    ISTORE x_piu_y

        LDC_W OBJREF
            ILOAD y
        INVOKEVIRTUAL fun2
    ISTORE fun2_y

        LDC_W OBJREF
            ILOAD x_piu_y
            ILOAD fun2_y
        INVOKEVIRTUAL fun1
    ISTORE fun1_xpiuy_fun2y

            ILOAD fun1_xpiuy_fun2y
            ILOAD fun2_y
        IADD
    ISTORE fun1_piu_fun2

    LDC_W OBJREF
        ILOAD fun1_piu_fun2
    INVOKEVIRTUAL print

    HALT
.end-main

.method fun1(x, y)
    .var
        j
    .end-var

        BIPUSH 1
    ISTORE j

    condizione:
            ILOAD j
            BIPUSH 4
        ISUB
    IFLT loop
    GOTO dopoloop

    loop:
        IINC y 3
        IINC j 1
    GOTO condizione
    dopoloop:

        LDC_W OBJREF
            ILOAD x
            BIPUSH 2
        INVOKEVIRTUAL mod
    IFEQ falso
    GOTO vero

    vero:
        IINC y 2
        IINC x -1
    GOTO dopoif

    falso:
        IINC x 1
        IINC y -1
    dopoif:

            ILOAD x
            ILOAD y
        IADD
    IRETURN
.end-method

.method fun2(b)
    condizione:
        LDC_W OBJREF
            ILOAD b
            BIPUSH 2
        INVOKEVIRTUAL mod
    IFEQ dopoloop
    GOTO loop

    loop:
        IINC b 1
    GOTO condizione
    dopoloop:

            BIPUSH 5
            ILOAD b
        ISUB
    IFLT vero
    GOTO falso

    vero:
        IINC b -2
    falso:

            BIPUSH 5
            ILOAD b
        ISUB
    IFEQ vero1
    GOTO falso1

    vero1:
            BIPUSH 6
        ISTORE b
    falso1:

            ILOAD b
            BIPUSH 3
        IADD
    IRETURN
.end-method

.method mul(i, j)
    ILOAD j
    IFEQ zero
    IINC j  -1
    ILOAD i
    IFEQ zero
    LDC_W OBJREF
    ILOAD i
    ILOAD j
    INVOKEVIRTUAL mul
    ILOAD i
	IADD
	GOTO fine
zero:
	BIPUSH 0x0
fine:     
	IRETURN
.end-method


.method mod (a, b)
	ILOAD a
	IFEQ stop
	ILOAD a
	ILOAD b
	ISUB
	IFLT stop
	LDC_W OBJREF
	ILOAD a
	ILOAD b
	ISUB
	ILOAD b
	INVOKEVIRTUAL mod
	GOTO fine
stop:      
	ILOAD a
fine:
	IRETURN
.end-method

.method div (a, b)    
	ILOAD a    
	ILOAD b        
	ISUB        
	IFLT stop    
	LDC_W OBJREF    
	ILOAD a         
	ILOAD b        
	ISUB        
	ILOAD b        
	INVOKEVIRTUAL div
	BIPUSH 1        
	IADD        
	GOTO fine
stop:        
	BIPUSH 0x0    
fine:        
	IRETURN
.end-method

.method input()
	.var
		num
	.end-var
	
	BIPUSH 0x0
	ISTORE num
	
loop:
	IN
	DUP
	
	LDC_W ENDLINE
	ISUB
	IFEQ stop
	
	BIPUSH 0x30
	ISUB
	
	LDC_W OBJREF
	ILOAD num
	BIPUSH 10
	INVOKEVIRTUAL mul
	
	IADD
	ISTORE num
	GOTO loop
	
stop:
	ILOAD num
	IRETURN
	
.end-method


.method print(x)
	ILOAD x
	IFEQ printz
	
	LDC_W OBJREF
	ILOAD x
	INVOKEVIRTUAL print_digits
	GOTO end
printz:
	BIPUSH 0x30
	OUT
end:

	BIPUSH 0
	IRETURN
.end-method

.method print_digits(x)
	.var
		char
	.end-var

	ILOAD x
	IFEQ end
	
	LDC_W OBJREF
	ILOAD x
	BIPUSH 10
	INVOKEVIRTUAL mod
	
	BIPUSH 0x30
	IADD
	ISTORE char
	
	LDC_W OBJREF
	ILOAD x
	BIPUSH 10
	INVOKEVIRTUAL div
	
	LDC_W OBJREF
	SWAP
	INVOKEVIRTUAL print_digits
	
	ILOAD char
	OUT

end:
	BIPUSH 0
	IRETURN
.end-method